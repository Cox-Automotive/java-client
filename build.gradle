apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'github-pages'

repositories {
    mavenLocal()
    mavenCentral()
}

allprojects {
    group = 'com.launchdarkly'
    version = "0.1.1"
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

dependencies {
    compile "org.apache.httpcomponents:httpclient:4.3.5"
    compile "org.apache.httpcomponents:httpclient-cache:4.3.5"
    compile "commons-codec:commons-codec:1.9"
    compile "com.google.code.gson:gson:2.2.4"
}

jar {
  baseName = 'launchdarkly-client'
  version = '0.1.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.9.0'
    }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            if (plugins.hasPlugin('war')) {
                from components.web
            } else {
                from components.java
            }

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

bintray {
    user= bintrayUser
    key = bintrayKey
    dryRun = false
    publish = true
    publications = ['mavenJava']
    pkg {
        repo = 'public'
        name="launchdarkly-java-client"
        userOrg = 'launchdarkly'
        desc = 'Java client library for LaunchDarkly'
        websiteUrl = 'http://launchdarkly.com'
        vcsUrl= 'https://github.com/launchdarkly/java-client'
        licenses=['Apache-2.0']
        labels=['launchdarkly', 'java', 'client', 'api']
        publicDownloadNumbers = true
    }
}

githubPages {
    repoUri = 'git@github.com:launchdarkly/java-client.git'
    pages {
        from javadoc.outputs.files
    }
    credentials {
        username = githubUser
        password = githubPassword
    }
}